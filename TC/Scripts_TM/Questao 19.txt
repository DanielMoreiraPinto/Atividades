//Syntax:

//-------CONFIGURATION
name: Resto - Questao 19
init: q0
accept: q10

//-------DELTA FUNCTION:
// Inicio
q0,*
q4,*,>

// Indo ao final da fita
q0,1
q0,1,>

q0,0
q1,0,>

q1,1
q1,1,>

q1,_
q2,_,<

q1,x
q2,x,<

// Marcando 1 do divisor e voltando ao dividendo
q2,1
q3,x,<

q2,x
q2,x,<

q2,0
q5,0,<

q3,1
q3,1,<

q3,0
q3,0,<

q3,_
q4,_,>

q3,x
q4,x,>

// Marcando 1 do dividendo e verificando se acabou a divisao
q4,1
q0,x,>

q4,0
q8,0,<

// Adicionando 1 no quociente e removendo os marcados do dividendo
// e desmarcando o divisor
q5,1
q5,1,<

q5,x
q5,x,<

q5,_
q6,_,>

q6,x
q6,_,>

q6,1
q6,1,>

q6,0
q7,0,>

q7,x
q7,1,>

q7,_
q2,_,<

// Voltando para o inicio e desmarcando os 1s do resto
q8,x
q8,1,<

q8,1
q8,1,<

q8,_
q9,_,>

// Deixando apenas resto na fita
q9,1
q9,1,>

q9,0
q10,_,>

q10,1
q10,_,>

q10,x
q10,_,>


//[current_state],[read_symbol]
//[new_state],[write_symbol],[>|<|-]

// < = left
// > = right
// - = hold
// use underscore for blank cells